#include "math.h"


kernel HDRIrradiance : ImageComputationKernel<ePixelWise>
{
    Image<eRead, eAccessRandom, eEdgeClamped> hdri; // the input image
    Image<eWrite> dst; // the output image

    param:
        int2 _samples;

    local:
        float2 __hdriPixelSize;
        float3 __up;
        float2 __sampleStep;


    /**
     * In define(), parameters can be given labels and default values.
     */
    void define()
    {
        defineParam(_samples, "Samples", int2(100, 50));
    }


    /**
     * The init() function is run before any calls to process().
     * Local variables can be initialized here.
     */
    void init()
    {
        __hdriPixelSize = float2(hdri.bounds.width() / (2.0f * PI), hdri.bounds.height() / PI);
        __up = float3(0, 1, 0);

        __sampleStep = float2(
            2.0f * PI / (float) _samples.x,
            PI / (2.0f * (float) _samples.y)
        );
    }


    /**
     * Get the value of hdri the ray would hit at infinite distance
     *
     * @arg rayDirection: The direction of the ray.
     *
     * @returns: The colour of the pixel in the direction of the ray.
     */
    float4 readHDRIValue(float3 rayDirection)
    {
        const float2 angles = cartesionUnitVectorToSpherical(rayDirection);

        // Why does bilinear give nans? :(
        return hdri(
            round(__hdriPixelSize.x * angles.x) - 1,
            round(hdri.bounds.height() - (__hdriPixelSize.y * angles.y)) - 1
        );
    }


    /**
     * Compute the irradiance of a pixel.
     * 
     * @arg pos: The x, and y location we are currently processing.
     */
    void process(int2 pos)
    {
        const float2 uvPosition = pixelsToUV(
            float2(pos.x, pos.y),
            float2(hdri.bounds.width(), hdri.bounds.height())
        );
        const float3 direction = sphericalUnitVectorToCartesion(
            uvPositionToAngles(uvPosition)
        );

        const float3 tangentRight = normalize(cross(__up, direction));
        const float3 tangentUp = normalize(cross(direction, tangentRight));

        float4 irradiance = float4(0);

        for (float theta = 0.0f; theta < 2.0f * PI; theta += __sampleStep.x)
        {
            for (float phi = PI / 2.0f; phi > 0.0f; phi -= __sampleStep.y)
            {
                const float3 tangent = sphericalUnitVectorToCartesion(float2(theta, phi));
                const float3 sampleDirection = (
                    tangent.x * tangentRight
                    + tangent.y * tangentUp
                    + tangent.z * direction
                );

                irradiance += readHDRIValue(sampleDirection) * cos(phi) * sin(phi);
            }
        }

        dst() = PI * irradiance / (float) (_samples.x * _samples.y);
    }
};
